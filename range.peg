package grange

type RangeQuery Peg {
  currentLiteral string
  nodeStack []Node
}

expression <- rangeexpr combinators? !.

rangeexpr <- space
    ( q
    / function
    / cluster
    / group
    / groupq
    / localkey
    / regex
    / value
    / { p.AddNull() } braces # Maybe a better way to match this?
    / null)

combinators <- space (union / intersect / exclude / braces)
intersect   <- '&' rangeexpr  { p.AddOperator(operatorIntersect) } combinators?
exclude     <- '-' rangeexpr  { p.AddOperator(operatorSubtract) } combinators?
union       <- ',' rangeexpr  { p.AddOperator(operatorUnion) } combinators?

# See https://github.com/pointlander/peg/issues/21 for context
braces  <- '{' rangeexpr combinators? '}' rangeexpr? { p.AddBraces() }

groupq  <- '?' rangeexpr { p.AddGroupQuery() }
cluster <- '%' rangeexpr { p.AddClusterLookup() } key?
group   <- '@' rangeexpr { p.AddGroupLookup() }

# TODO: Use rangeexpr for the following?
key      <- ':' literal { p.AddKeyLookup(buffer[begin:end]) }
localkey <- '$' literal { p.AddLocalClusterLookup(buffer[begin:end]) }

function <- literal { p.AddFunction(buffer[begin:end]) } '(' funcargs ')'
funcargs <- rangeexpr { p.AddFuncArg() } ';' funcargs
          / rangeexpr { p.AddFuncArg() }

regex   <- '/' < (!'/' .)* > '/' { p.AddRegex(buffer[begin:end]) }
literal <- <[[a-z0-9-_]]+>
value   <- <[[a-z0-9-_.]]+> { p.AddValue(buffer[begin:end]) }
space   <- ' '*
q       <- 'q(' <(!')' .)*> ')' { p.AddConstant(buffer[begin:end]) }
